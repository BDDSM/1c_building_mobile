
#Область ПрограммныйИнтерфейс

// Возвращает идентификатор приложения в базе статистики.
// 
// Возвращаемое значение:
//  Строка - Идентификатор приложения в базе статистики.
//
Функция ПолучитьИдентификаторПриложения() Экспорт

	Возврат "135ac50a-c4d0-11e5-1598-0050569f4ccd";

КонецФункции

// Возвращает идентификатор устройства (базы) для сбора статистики.
// 
// Возвращаемое значение:
//  Строка - Идентификатор устройства.
//
Функция ПолучитьИдентификаторЭкземпляра() Экспорт

	Возврат Строка(ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("КодУстройства"));

КонецФункции

// Выполняет сбор статистики для показателей которые собираются в момент старта.
//
Процедура СобратьДанныеСтатистикиПриОбновлении() Экспорт

	СисИнфо = Новый СистемнаяИнформация();

	// записываем показатели которые должны менять только при изменении
	СборСтатистикиВызовСервера.ЗаписатьПоказатель("Окружение.ВерсияОС", СисИнфо.ВерсияОС);
	СборСтатистикиВызовСервера.ЗаписатьПоказатель("Окружение.ЯзыкСистемы", ТекущийЯзыкСистемы());
	СборСтатистикиВызовСервера.ЗаписатьПоказатель("Окружение.ДиагональЭкрана", 
		ЭкранКлиентСервер.ДиагональЭкрана());

	СборСтатистикиВызовСервера.ЗаписатьПоказатель("Приложение.ВерсияКлиента", 
		ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ВерсияДокументооборота"));

	Если ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ИспользоватьПароль") 
		И Не ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ХешПароля") = 0 Тогда
		СборСтатистикиВызовСервера.ЗаписатьПоказатель("Параметры.УстановленПароль");
	КонецЕсли;

	СборСтатистикиВызовСервера.ЗаписатьПоказатель("Параметры.Протокол", ПолучитьТипПодключения());

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

Функция ПолучитьТипПодключения()
	
	СтрокаПодключения = ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("АдресЦентральнойБазы");
	ПозицияРазделителя = СтрНайти(СтрокаПодключения, ":");

	Возврат НРег(Лев(СтрокаПодключения, ПозицияРазделителя - 1));

КонецФункции

#КонецОбласти