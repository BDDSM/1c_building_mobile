
#Область ПрограммныйИнтерфейс

// Начинает процедуру изменения флага объекта.
//
// Параметры:
//  ЭтаФорма				 - Форма - Вызывающая форма
//  Объект					 - СправочникСсылка.Задачи - Ссылка на объект;
//  МодульОбработки			 - Модуль - Общий модуль;
//  ИмяПроцедурыОбработчика	 - Строка - Имя процедуры обрабатывающей выбор из списка;
//  ЦветаИсключения			 - Массив - Исключения, не добавляемые в список команд.
//
Процедура ИзменитьФлаг(ЭтаФорма, Объект, МодульОбработки, ИмяПроцедурыОбработчика, 
	ЦветаИсключения = Неопределено) Экспорт

	// Настраиваем список доступных команд
	СписокДействийСтроки = Новый СписокЗначений();

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Красный"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыКрасный(),
		БиблиотекаКартинок.КомандаЦветКрасный);

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Синий"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыСиний(),
		БиблиотекаКартинок.КомандаЦветСиний);

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Желтый"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыЖелтый(),
		БиблиотекаКартинок.КомандаЦветЖелтый);

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Зеленый"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыЗеленый(),
		БиблиотекаКартинок.КомандаЦветЗеленый);

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Оранжевый"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыОранжевый(),
		БиблиотекаКартинок.КомандаЦветОранжевый);

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Лиловый"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыЛиловый(),
		БиблиотекаКартинок.КомандаЦветЛиловый);

	ДобавитьКомандуВСписок(ЦветаИсключения, СписокДействийСтроки, 
		ПредопределенноеЗначение("Перечисление.ЦветаОбъектов.Нет"),
		ТекстыСообщенийКлиентСерверПовтИсп.ЗаголовокКомандыОчистить(),
		БиблиотекаКартинок.Заглушка);

	// Настраиваем обработчик выбора пользователя
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("Объект", Объект);
	ДопПараметры.Вставить("ЭтаФорма", ЭтаФорма);

	ОбработчикЗавершения = Новый ОписаниеОповещения(ИмяПроцедурыОбработчика, 
		МодульОбработки, ДопПараметры);

	// Показываем пользователю команды в списке.
	ЭтаФорма.ПоказатьВыборИзСписка(ОбработчикЗавершения, СписокДействийСтроки);

КонецПроцедуры

// Обработчик выбора пользователя команды "Цвет" в меню "Еще".
//
// Параметры:
//  Ответ		 - Произвольный - Ответ полученный у пользователя;
//  ДопПараметры - Произвольный - Дополнительные параметры вызова.
//
Процедура ОбработатьВыборКомандыЦветВМенюЕще(Ответ, ДопПараметры) Экспорт

	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Результат = Ответ.Значение;

	ФлагиОбъектовВызовСервера.УстановитьФлагОбъекта(ДопПараметры.Объект, Результат);
	
	Оповестить("ПоявилисьНеотправленныеДанные");
	Оповестить("УстановленФлаг", ДопПараметры.Объект, ДопПараметры.ЭтаФорма);

КонецПроцедуры

// Устанавливает новый флаг у объекта
//
// Параметры:
//  Элементы  - ЭлементыФормы - Элементы формы инициатора;
//  Объект    - Объект        - Объект у которого устанавливается флаг;
//  НовыйФлаг - Строка        - Идентификатор значения перечисления;
//  Флаг      - Перечисление.ЦветаОбъектов - Заданное значение флага.
//
Процедура УстановитьНовыйФлаг(Элементы, Объект, НовыйФлаг, Флаг = Неопределено) Экспорт

	ИмяФлага = СтрШаблон(
		"Перечисление.ЦветаОбъектов.%1", НовыйФлаг);

	ЗначениеФлага = ПредопределенноеЗначение(ИмяФлага);

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда

		ФлагиОбъектовВызовСервера.УстановитьФлагОбъекта(Объект.Ссылка, ЗначениеФлага);

		Оповестить("ПоявилисьНеотправленныеДанные");

	Иначе

		Флаг = ЗначениеФлага;

	КонецЕсли;

	Элементы.КомандыФлагов.Картинка = ФлагиОбъектовПовтИсп.ПолучитьКартинкуПоФлагу(ЗначениеФлага);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКомандуВСписок(Исключения, Список, Цвет, Заголовок, Картинка)

	Если Не Исключения = Неопределено 
		И Не Исключения.Найти(Цвет) = Неопределено Тогда

		Возврат;

	КонецЕсли;

	Список.Добавить(Цвет, Заголовок, , Картинка);

КонецПроцедуры

#КонецОбласти
