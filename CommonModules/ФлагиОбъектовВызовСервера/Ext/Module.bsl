
#Область ПрограммныйИнтерфейс

// Получает флаг указанному объекта
//
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка.Задачи - ссылка на задачу;
//                 - ДокументСсылка.ВходящееПисьмо - Ссылка на входящее письмо;
//                 - ДокументСсылка.ИсходящееПисьмо - Ссылка на исходящее письмо.
// 
// Возвращаемое значение:
//  Флаг - ПеречислениеСсылка.ЦветаОбъектов.
//
Функция ПолучитьФлагОбъекта(СсылкаНаОбъект) Экспорт

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Объект", СсылкаНаОбъект);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФлагиОбъектов.Флаг
		|ИЗ
		|	РегистрСведений.ФлагиОбъектов КАК ФлагиОбъектов
		|ГДЕ
		|	ФлагиОбъектов.Объект = &Объект";

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Флаг = Выборка.Флаг;
	Иначе
		Флаг = Перечисления.ЦветаОбъектов.ПустаяСсылка();
	КонецЕсли;

	Возврат Флаг;

КонецФункции

// Устанавливает указанному объекту флаг.
//
// Параметры:
//  Объект	 - СсылкаНаОбъект - Ссылка на объект;
//  Флаг	 - Перечисление.ФлагиОбъектов - Значение флага объекта.
//
Процедура УстановитьФлагОбъекта(Объект, Флаг) Экспорт

	// Записываем флаг объекта.
	Набор = РегистрыСведений.ФлагиОбъектов.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);

	Запись = Набор.Добавить();
	Запись.Объект = Объект;
	Запись.Флаг = Флаг;

	Набор.Записать(Истина);

	// В случае постановки задачи на исполнение самому себе нужно
	// сменить флаг у родительского процесса - как в настольном ДО.
	ДополнительныйОбъект = Неопределено;

	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Задачи") 
		И ЗначениеЗаполнено(Объект.ПроцессИсполнение) Тогда
		ДополнительныйОбъект = Объект.ПроцессИсполнение;
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПроцессыИсполнение") Тогда

		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Процесс", Объект);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Задачи.Ссылка
			|ИЗ
			|	Справочник.Задачи КАК Задачи
			|ГДЕ
			|	Задачи.ПроцессИсполнение = &Процесс";

		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ДополнительныйОбъект = Выборка.Ссылка;
		КонецЕсли;

	КонецЕсли;

	Если Не ЗначениеЗаполнено(ДополнительныйОбъект) Тогда
		Возврат;
	КонецЕсли;

	Набор  = РегистрыСведений.ФлагиОбъектов.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(ДополнительныйОбъект);

	Запись = Набор.Добавить();
	Запись.Объект = ДополнительныйОбъект;
	Запись.Флаг = Флаг;

	Набор.Записать(Истина);

КонецПроцедуры

#КонецОбласти
