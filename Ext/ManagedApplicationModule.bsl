
#Область ОпределениеПеременных

Перем ЭтоЧистаяБаза; // Признак того, что база только что создана, или полностью очищена
Перем ТекущийРаздел; // Текущий  используемый раздел программы

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередНачаломРаботыСистемы(Отказ)

	// Получим имя каталога временных файлов
	ИмяКаталогаВременныхФайлов = ПолучитьИмяКаталогаВременныхФайлов();

	Параметры = ОбщегоНазначенияВызовСервера.УстановитьПервоначальныеНастройки(
		ИмяКаталогаВременныхФайлов);

	ЭтоЧистаяБаза = Параметры.ЭтоЧистаяБаза;
	ТекущийРаздел = Параметры.ТекущийРаздел;

КонецПроцедуры

Процедура ПриНачалеРаботыСистемы()

	СборСтатистикиВызовСервера.ЗаписатьПоказатель("Приложение.Запуск");

	Если ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ИспользоватьПароль") 
		И Не ОбщегоНазначенияВызовСервера.ПолучитьЗначениеКонстанты("ХешПароля") = 0 Тогда

		ПараметрыОбработчика = Новый Структура();
		ПараметрыОбработчика.Вставить("ЭтоЧистаяБаза", ЭтоЧистаяБаза);
		ПараметрыОбработчика.Вставить("ТекущийРаздел", ТекущийРаздел);

		ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ОбработчикЗакрытияФормыВводаPINКода", 
			ОбщегоНазначенияКлиент, ПараметрыОбработчика);

		ПараметрыОткрытияФормы = Новый Структура();
		ПараметрыОткрытияФормы.Вставить("Команда", "ВвестиПароль");
		Результат = ОткрытьФорму("ОбщаяФорма.ВводПароля", ПараметрыОткрытияФормы,,,,,
			ОбработчикЗакрытияФормы, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

		Возврат;

	КонецЕсли;

	ОбщегоНазначенияКлиент.ВыполнитьДействияПриЗапускеСистемы(ЭтоЧистаяБаза, ТекущийРаздел);

	ПодключитьОбработчикОповещения("ОбработкаОбщихОповещений");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьИмяКаталогаВременныхФайлов()

	ИмяКаталогаВременныхФайлов = СокрЛП(КаталогДокументов());
	Если СтрЗаканчиваетсяНа(ИмяКаталогаВременныхФайлов, "\")
	 Или СтрЗаканчиваетсяНа(ИмяКаталогаВременныхФайлов, "/") Тогда
		ИмяКаталогаВременныхФайлов = ИмяКаталогаВременныхФайлов + "temp";
	ИначеЕсли СтрНайти(ИмяКаталогаВременныхФайлов, "\") > 0 Тогда
		ИмяКаталогаВременныхФайлов = ИмяКаталогаВременныхФайлов + "\temp";
	ИначеЕсли СтрНайти(ИмяКаталогаВременныхФайлов, "/") > 0 Тогда
		ИмяКаталогаВременныхФайлов = ИмяКаталогаВременныхФайлов + "/temp";
	КонецЕсли;
	
	Возврат ИмяКаталогаВременныхФайлов;

КонецФункции

Процедура ОбработкаОбщихОповещений(ИмяСобытия, Параметр, Источник) Экспорт
	
	Если ИмяСобытия = "СинхронизацияЗавершена" Тогда
		ОчисткаБазыДанныхВызовСервера.ЗапуститьОчисткуОтСтарыхДанныхВФоне();
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти