
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтрокаТаб = ТаблицаКоманд.Добавить();
	СтрокаТаб.Команда = "ГруппаФэйсбук";
	СтрокаТаб.ОписаниеКоманды = 
		НСтр("ru = 'Наша группа Facebook'; en = 'Follow us on Facebook'");
	СтрокаТаб.Пояснение = 
		НСтр("ru = 'Присоединяйтесь к живому общению вместе с вашими коллегами и командой разработки в социальной сети Facebook.'");

	СтрокаТаб = ТаблицаКоманд.Добавить();
	СтрокаТаб.Команда = "ГруппаВКонтакте";
	СтрокаТаб.ОписаниеКоманды = 
		НСтр("ru = 'Наша группа ВКонтакте'; en = 'Follow us on VKontakte'");
	СтрокаТаб.Пояснение = 
		НСтр("ru = 'Присоединяйтесь к живому общению вместе с вашими коллегами и командой разработки в социальной сети ВКонтакте.'");

	СтрокаТаб = ТаблицаКоманд.Добавить();
	СтрокаТаб.Команда = "НаписатьОтзыв";

	ТипОС = ВРег(Константы.ИмяОперационнойСистемы.Получить());
	Если ТипОС = "IOS" Тогда
		СсылкаДляОтзываВМагазине = 
			"itms-apps://itunes.apple.com/ru/app/1c-dokumentooborot/id1119102464?l=ru&ls=1&mt=8";

		СтрокаТаб.ОписаниеКоманды = 
			НСтр("ru = 'Оставить отзыв в Apple AppStore'; en = 'Rate in Apple AppStore'");

	ИначеЕсли ТипОС = "ANDROID" Тогда
		СсылкаДляОтзываВМагазине = 
			"market://details?id=com.e1c.mobiledocman21";

		СтрокаТаб.ОписаниеКоманды = 
			НСтр("ru = 'Оставить отзыв в Google Play'; en = 'Rate in Google Play'");

	Иначе
		СсылкаДляОтзываВМагазине = 
			"https://www.microsoft.com/ru-ru/store/apps/1С-Документооборот-21/9nblggh4wrkz";

		СтрокаТаб.ОписаниеКоманды = 
			НСтр("ru = 'Оставить отзыв в WindowsPhone Store'; en = 'Rate in WindowsPhone Store'");

	КонецЕсли;

	СтрокаТаб.Пояснение = 
		НСтр("ru = 'Оставьте оценку и отзыв в магазине приложений.'");

	СтрокаТаб = ТаблицаКоманд.Добавить();
	СтрокаТаб.Команда = "НаписатьРазработчикам";
	СтрокаТаб.ОписаниеКоманды = 
		НСтр("ru = 'Написать разработчикам'; en = 'Send feedback'");
	СтрокаТаб.Пояснение = 
		НСтр("ru = 'Напишите разработчикам программы письмо с описанием ошибки, проблемы или предложением.'");

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()

	ПараметрыЭкрана = ЭкранКлиентСервер.ПараметрыЭкрана();
	ШиринаЭкрана    = ПараметрыЭкрана[0].Ширина;
	ВысотаЭкрана    = ПараметрыЭкрана[0].Высота;
	
	Если ШиринаЭкрана > ВысотаЭкрана Тогда
		Элементы.ТаблицаКомандПояснение.Высота = 2;
	Иначе
		Элементы.ТаблицаКомандПояснение.Высота = 3;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаКомандВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ТекущиеДанные = Элементы.ТаблицаКоманд.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТекущиеДанные.Команда = "ГруппаФэйсбук" Тогда
		ЗапуститьПриложение("http://www.facebook.com/groups/1C.DOC8");

	ИначеЕсли ТекущиеДанные.Команда = "ГруппаВКонтакте" Тогда
		ЗапуститьПриложение("http://vk.com/1c.doc8");

	ИначеЕсли ТекущиеДанные.Команда = "НаписатьОтзыв" Тогда
		ЗапуститьПриложение(СсылкаДляОтзываВМагазине);

	ИначеЕсли ТекущиеДанные.Команда = "НаписатьРазработчикам" Тогда
		ОткрытьФорму("ОбщаяФорма.СообщениеРазработчикам");

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
